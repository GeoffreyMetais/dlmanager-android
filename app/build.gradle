apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kotlin {
    experimental {
        coroutines 'enable'
    }
}

android {
    compileSdkVersion 27
    dataBinding.enabled = true
    defaultConfig {
        applicationId "org.gmetais.downloadmanager"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        //Load local properties
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        buildConfigField "String", "API_URL", "\"${properties.getProperty("api.url")}\""
        buildConfigField "String", "API_USERNAME", "\"${properties.getProperty('api.username')}\""
        buildConfigField "String", "API_SECRET", "\"${properties.getProperty('api.secret')}\""
        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    // used by Room, to test migrations
//    sourceSets {
//        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
//    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':tools')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.ext.kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.kotlinx_version"
    //AppCompat
    implementation "com.android.support:appcompat-v7:$rootProject.ext.appCompatVersion"
    implementation "com.android.support:support-fragment:$rootProject.ext.appCompatVersion"
    implementation "com.android.support:design:$rootProject.ext.appCompatVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.ext.appCompatVersion"
    implementation "com.android.support:preference-v7:$rootProject.ext.appCompatVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //Arch Components
    implementation "android.arch.lifecycle:extensions:$rootProject.ext.archVersion"
    kapt "android.arch.lifecycle:compiler:$rootProject.ext.archVersion"
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.roomVersion"
    kapt "android.arch.persistence.room:compiler:$rootProject.ext.roomVersion"
    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    //Testing
    //    androidTestImplementation "android.arch.persistence.room:testing:$rootProject.ext.archVersion"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

